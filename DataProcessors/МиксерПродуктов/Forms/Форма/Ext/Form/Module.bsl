&НаКлиенте
Процедура ПродуктПриИзменении(Элемент)
	ПродуктПриИзмененииНаСервере();
КонецПроцедуры 

&НаКлиенте
Процедура ПродуктыПриИзменении(Элемент)
	ПродуктыПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СреднееАрифметическое(Команда) 	
	
	
	ИмяГруппы 	= Элементы.ПравилаОбъединенияГруппаДанныеПродуктов.Имя;
	Количество 	= Элементы.ПравилаОбъединенияГруппаДанныеПродуктов.ПодчиненныеЭлементы.Количество();
	
	ФормулаРасчета = "(";
	Для каждого Ит Из Элементы.ПравилаОбъединенияГруппаДанныеПродуктов.ПодчиненныеЭлементы Цикл
		ФормулаРасчета = ФормулаРасчета + СтрЗаменить(Ит.Имя, ИмяГруппы, "") + "+";		 	
	КонецЦикла;
	ФормулаРасчета = Лев(ФормулаРасчета, СтрДлина(ФормулаРасчета) - 1);
	ФормулаРасчета = ФормулаРасчета + ") / " + Количество;	
	
	Для каждого Ит Из Элементы.ПравилаОбъединения.ВыделенныеСтроки Цикл
		ТекСтрока = Элементы.ПравилаОбъединения.ДанныеСтроки(Ит);
		ТекСтрока.Правило = ФормулаРасчета;	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СреднееКвадратичное(Команда)
	
	ИмяГруппы 	= Элементы.ПравилаОбъединенияГруппаДанныеПродуктов.Имя;
	Количество 	= Элементы.ПравилаОбъединенияГруппаДанныеПродуктов.ПодчиненныеЭлементы.Количество();
	
	ФормулаРасчета = "SQRT((";
	Для каждого Ит Из Элементы.ПравилаОбъединенияГруппаДанныеПродуктов.ПодчиненныеЭлементы Цикл
		ФормулаРасчета = ФормулаРасчета + СтрЗаменить(Ит.Имя, ИмяГруппы, "") + "*" + СтрЗаменить(Ит.Имя, ИмяГруппы, "") + "+";		 	
	КонецЦикла;
	ФормулаРасчета = Лев(ФормулаРасчета, СтрДлина(ФормулаРасчета) - 1);
	ФормулаРасчета = ФормулаРасчета + ") / " + Количество + ")";	
	
	Для каждого Ит Из Элементы.ПравилаОбъединения.ВыделенныеСтроки Цикл
		ТекСтрока = Элементы.ПравилаОбъединения.ДанныеСтроки(Ит);
		ТекСтрока.Правило = ФормулаРасчета;	
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВзятьПоказатель(Команда)
	
	Перем ФормулаРасчета;
	
	ПоказатьВводЗначения(Новый ОписаниеОповещения("ВзятьПоказательЗавершение", ЭтаФорма), ФормулаРасчета, "Введите значение",Тип("Строка"));	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьПродукт(Команда)
	ЗаписатьПродуктНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПродуктыПриИзмененииНаСервере()
	
	УстановитьИменаПараметровПродуктов();
	СформироватьКолонкиДанныеПоПродуктам();
	ЗаполнитьТаблицуПравил();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИменаПараметровПродуктов()
	
	Для каждого Ит Из Объект.Продукты Цикл
		Ит.ИмяПараметра = "П" + Ит.НомерСтроки;	
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура СформироватьКолонкиДанныеПоПродуктам()  	
	
	МассивТипов					= Новый Массив;		
	ПодчиненныеЭлементыГруппы 	= Элементы.ПравилаОбъединенияГруппаДанныеПродуктов.ПодчиненныеЭлементы;
	МассивУдаляемыхРеквизитов	= Новый Массив; 
	МассивДобавляемыхРеквизитов = Новый Массив;
	ИмяГруппы 					= Элементы.ПравилаОбъединенияГруппаДанныеПродуктов.Имя;
	ПутьКРеквизиту				= "Объект.ПравилаОбъединения"; 	
	
	МассивТипов.Добавить(Тип("Строка"));
	МассивТипов.Добавить(Тип("Число"));
	
	ОписаниеТипа = Новый ОписаниеТипов(МассивТипов, Новый КвалификаторыЧисла(15, 8), Новый КвалификаторыСтроки(255));
	
	Пока ПодчиненныеЭлементыГруппы.Количество() > 0 Цикл
		МассивУдаляемыхРеквизитов.Добавить(ПодчиненныеЭлементыГруппы[0].ПутьКДанным);
		Элементы.Удалить(ПодчиненныеЭлементыГруппы[0]);				
	КонецЦикла;   	 
	
	Для каждого Ит Из Объект.Продукты Цикл 
		
		Если НЕ ЗначениеЗаполнено(Ит.Продукт) Тогда
			Продолжить;	
		КонецЕсли; 
		
		ИмяРеквизита = Ит.ИмяПараметра;
		Заголовок    = Строка(Ит.Продукт);
		
		МассивДобавляемыхРеквизитов.Добавить(Новый РеквизитФормы(ИмяРеквизита, ОписаниеТипа, ПутьКРеквизиту, Заголовок));
				
	КонецЦикла;	
	
	ИзменитьРеквизиты(МассивДобавляемыхРеквизитов, МассивУдаляемыхРеквизитов); 
	
	Для каждого Ит Из Объект.Продукты Цикл 
		
		Если НЕ ЗначениеЗаполнено(Ит.Продукт) Тогда
			Продолжить;	
		КонецЕсли; 
		
		ИмяРеквизита = Ит.ИмяПараметра;
		Заголовок    = Строка(Ит.Продукт); 
		
		Показатель 						= Элементы.Вставить(ИмяГруппы + ИмяРеквизита, Тип("ПолеФормы"), Элементы.ПравилаОбъединенияГруппаДанныеПродуктов);
		Показатель.Вид					= ВидПоляФормы.ПолеВвода; 
		Показатель.ПутьКДанным			= ПутьКРеквизиту + "." + ИмяРеквизита; 		
		
	КонецЦикла;
	
КонецПроцедуры
 
&НаСервере
Процедура ЗаполнитьТаблицуПравил()	
	
	Объект.ПравилаОбъединения.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(Продукты.Продукт КАК Справочник.ПродуктыПитания) КАК Продукт,
		|	Продукты.ИмяПараметра
		|ПОМЕСТИТЬ Вт_Таблица
		|ИЗ
		|	&Продукты КАК Продукты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Продукты.Продукт КАК Продукт,
		|	Продукты.ИмяПараметра
		|ПОМЕСТИТЬ Вт_Продукты
		|ИЗ
		|	Вт_Таблица КАК Продукты
		|ГДЕ
		|	Продукты.Продукт <> ЗНАЧЕНИЕ(Справочник.ПродуктыПитания.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПродуктыПитания.Ссылка,
		|	""ТекЗначение""
		|ИЗ
		|	Справочник.ПродуктыПитания КАК ПродуктыПитания
		|ГДЕ
		|	ПродуктыПитания.Ссылка = &Продукт
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Вт_Продукты.Продукт,
		|	Вт_Продукты.ИмяПараметра КАК ИмяПараметра,
		|	ХимСостав.Показатель КАК Показатель,
		|	ХимСостав.Значение
		|ИЗ
		|	(ВЫБРАТЬ
		|		ХимическийСоставПродуктов.Продукт КАК Продукт,
		|		ХимическийСоставПродуктов.Нутриент КАК Показатель,
		|		ХимическийСоставПродуктов.Количество КАК Значение
		|	ИЗ
		|		РегистрСведений.ХимическийСоставПродуктов КАК ХимическийСоставПродуктов
		|	ГДЕ
		|		ХимическийСоставПродуктов.Продукт В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					Вт_Продукты.Продукт
		|				ИЗ
		|					Вт_Продукты КАК Вт_Продукты)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Вт_Продукты.Продукт,
		|		""Наименование"",
		|		ПРЕДСТАВЛЕНИЕ(Вт_Продукты.Продукт)
		|	ИЗ
		|		Вт_Продукты КАК Вт_Продукты
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Вт_Продукты.Продукт,
		|		""Белки"",
		|		Вт_Продукты.Продукт.Белки
		|	ИЗ
		|		Вт_Продукты КАК Вт_Продукты
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Вт_Продукты.Продукт,
		|		""Жиры"",
		|		Вт_Продукты.Продукт.Жиры
		|	ИЗ
		|		Вт_Продукты КАК Вт_Продукты
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Вт_Продукты.Продукт,
		|		""Углеводы"",
		|		Вт_Продукты.Продукт.Углеводы
		|	ИЗ
		|		Вт_Продукты КАК Вт_Продукты
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Вт_Продукты.Продукт,
		|		""Калорийность"",
		|		Вт_Продукты.Продукт.Калорийность
		|	ИЗ
		|		Вт_Продукты КАК Вт_Продукты) КАК ХимСостав
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Вт_Продукты КАК Вт_Продукты
		|		ПО ХимСостав.Продукт = Вт_Продукты.Продукт
		|
		|УПОРЯДОЧИТЬ ПО
		|	Показатель
		|ИТОГИ ПО
		|	Показатель";
		
	Запрос.УстановитьПараметр("Продукты", Объект.Продукты.Выгрузить());	
	Запрос.УстановитьПараметр("Продукт", Объект.Продукт);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаПоказатели = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоказатели.Следующий() Цикл
		
		НоваяСтрока 			= Объект.ПравилаОбъединения.Добавить();
		НоваяСтрока.Параметр 	= ВыборкаПоказатели.Показатель;
		
		ВыборкаДетальныеЗаписи = ВыборкаПоказатели.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл		
			НоваяСтрока[ВыборкаДетальныеЗаписи.ИмяПараметра] = ВыборкаДетальныеЗаписи.Значение;			
		КонецЦикла;
		
	КонецЦикла; 	
	
КонецПроцедуры

&НаКлиенте
Процедура ВзятьПоказательЗавершение(Значение, ДополнительныеПараметры) Экспорт
	
	Если Значение <> Неопределено Тогда
		
		Для каждого Ит Из Элементы.ПравилаОбъединения.ВыделенныеСтроки Цикл
			ТекСтрока 			= Элементы.ПравилаОбъединения.ДанныеСтроки(Ит);
			ТекСтрока.Правило 	= Значение;	
		КонецЦикла;
		
	КонецЕсли; 	
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьПродуктНаСервере()

	Перем Результат, ПродуктСсылка, ПродуктОбъект;
	ДанныеЗаполнения 	= Новый Структура("Источник, Родитель", Справочники.ИсточникиИнформации.ЭТАЛОН, Объект.Родитель);
	П				 	= Новый Структура;
	ПродуктПустой 		= НЕ ЗначениеЗаполнено(Объект.Продукт);
		
	Если ПродуктПустой Тогда
		ПродуктСсылка  = Справочники.ПродуктыПитания.ПолучитьСсылку();
		ПродуктОбъект  = Справочники.ПродуктыПитания.СоздатьЭлемент();
		ПродуктОбъект.УстановитьСсылкуНового(ПродуктСсылка); 		 		
	Иначе
		ПродуктСсылка = Объект.Продукт;
		ПродуктОбъект = Объект.Продукт.ПолучитьОбъект();
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ХимическийСоставПродуктов.СоздатьНаборЗаписей(); 	
	
	Для каждого Ит Из Объект.ПравилаОбъединения Цикл  		  		
		
		Если Не ЗначениеЗаполнено(Ит.Правило) Тогда
			Продолжить;	
		КонецЕсли; 
		
		П.Очистить();
		Результат		 = "";
		ТЗ 				 = Ит.ТекЗначение;
		ПП 				 = Ит.ПроизвольныйПараметр;		
		ФормулаРасчета 	 = ПолучитьФормулуРасчета(Ит, П);
		
		Попытка
			Выполнить(ФормулаРасчета);	
		Исключение			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не возможно выполнить формулу расчета в строке " + Ит.НомерСтроки;
			Сообщение.Сообщить();				   
	   КонецПопытки;  		
		
		Если ТипЗнч(Ит.Параметр) = Тип("Строка") Тогда
			ДанныеЗаполнения.Вставить(Ит.Параметр, Результат);		
		Иначе
			Запись 				= НаборЗаписей.Добавить();
			Запись.Продукт 		= ПродуктСсылка;
			Запись.Нутриент 	= Ит.Параметр;
			Запись.Количество 	= Результат;
		КонецЕсли;		
		
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(ПродуктОбъект, ДанныеЗаполнения); 	 	 		
	ПродуктОбъект.Записать(); 
	
	НаборЗаписей.Отбор.Продукт.Установить(ПродуктСсылка);
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьФормулуРасчета(Знач ТекущаяСтрока, СтруктураПараметров)
	
	Результат 					= ТекущаяСтрока.Правило; 	
	ПодчиненныеЭлементыГруппы 	= Элементы.ПравилаОбъединенияГруппаДанныеПродуктов.ПодчиненныеЭлементы;	
	
	Для каждого Ит Из ПодчиненныеЭлементыГруппы Цикл  		
		ИмяПараметра = СтрЗаменить(Ит.Имя, "ПравилаОбъединенияГруппаДанныеПродуктов", "");		
		Результат 	 = СтрЗаменить(Результат, ИмяПараметра, "П." + ИмяПараметра);
		СтруктураПараметров.Вставить(ИмяПараметра, ?(ЗначениеЗаполнено(ТекущаяСтрока[ИмяПараметра]), ТекущаяСтрока[ИмяПараметра], 0));
	КонецЦикла;
	
	Возврат "Результат = " + Результат;
	
КонецФункции 

&НаСервере
Процедура ПродуктПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Продукт) Тогда
		Объект.Родитель = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Объект.Продукт, "Родитель");
		Объект.Источник = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Объект.Продукт, "Источник");	
	КонецЕсли; 	
	
	ЗаполнитьТаблицуПравил();   
	
КонецПроцедуры

&НаКлиенте
Процедура ПравилаОбъединенияПравилоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыРасчета	 = Новый Соответствие;
	ТекущаяСтрока 		 = Элементы.ПравилаОбъединения.ТекущиеДанные;
	
	Если ТекущаяСтрока <> Неопределено Тогда 	 
	
		Для каждого Ит Из Объект.Продукты Цикл
			ПараметрыРасчета.Вставить(Ит.Продукт, Ит.ИмяПараметра);	
		КонецЦикла; 
		
		ОткрытьФорму("Обработка.МиксерПродуктов.Форма.ФормаПолученияФормулы", Новый Структура("ПараметрыРасчета", ПараметрыРасчета), ЭтаФорма,,,, Новый ОписаниеОповещения("ПравилаОбъединенияПравилоНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("ТекущаяСтрока", ТекущаяСтрока)), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);	
	
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПравилаОбъединенияПравилоНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ТекущаяСтрока = ДополнительныеПараметры.ТекущаяСтрока; 
		ТекущаяСтрока.Правило = Результат;	
	КонецЕсли;   	
	
КонецПроцедуры

 


  

